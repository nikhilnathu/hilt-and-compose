plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'de.mannodermaus.android-junit5'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.example.hiltandcompose"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", getApiKey()
            buildConfigField "int", "FOO", "42"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }

    hilt {
        enableTransformForLocalTests = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation libraries

    kapt kaptLibraries

    testImplementation testLibraries
    testRuntimeOnly runtimeTestLibraries
    androidTestImplementation androidTestLibraries

    kaptTest kaptTestLibraries
}

String getApiKey() {
    def propFile = rootProject.file("./local.properties")
    def properties = new Properties()
    if (propFile.canRead()) properties.load(new FileInputStream(propFile))
    return properties['BASE_URL']
}